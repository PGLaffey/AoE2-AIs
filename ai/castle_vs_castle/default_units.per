; Units everyone has access to
(defrule 
    (true)
    =>
    (set-goal g-swordsman-unit militiaman-line)
    (set-goal g-spearman-unit spearman-line)
    (set-goal g-archer-unit archer-line)
    (set-goal g-skirmisher-unit skirmisher-line)
    (set-goal g-unique-unit my-unique-unit-line)
    (disable-self)
)

; Alt Infantry
(defrule
    (can-train eagle-warrior-line)
    =>
    (set-goal g-alt-infantry-unit eagle-warrior-line)
    (disable-self)
)
(defrule
    (not
        (can-train eagle-warrior-line)
    )
    (can-train fire-lancer-line)
    =>
    (set-goal g-alt-infantry-unit fire-lancer-line)
    (disable-self)
)
; Cavalry Archer
(defrule
    (can-train cavalry-archer-line)
    =>
    (set-goal g-cavalry-archer-unit cavalry-archer-line)
    (disable-self)
)
(defrule
    (not
        (can-train cavalry-archer-line)
    )
    (can-train elephant-archer-line)
    =>
    (set-goal g-cavalry-archer-unit elephant-archer-line)
    (disable-self)
)
; Light Cavalry
(defrule
    (can-train scout-cavalry-line)
    =>
    (set-goal g-light-cavalry-unit scout-cavalry-line)
    (disable-self)
)
; Heavy Cavalry
(defrule
    (can-train knight-line)
    =>
    (set-goal g-heavy-cavalry-unit knight-line)
    (disable-self)
)
(defrule
    (not(can-train knight-line))
    (can-train hei-guang-cavalry-line)
    =>
    (set-goal g-heavy-cavalry-unit hei-guang-cavalry-line)
    (disable-self)
)
(defrule
    (not(can-train knight-line))
    (not(can-train hei-guang-cavalry-line))
    (can-train steppe-lancer-line)
    =>
    (set-goal g-heavy-cavalry-unit steppe-lancer-line)
    (disable-self)
)
(defrule
    (not(can-train knight-line))
    (not(can-train hei-guang-cavalry-line))
    (not(can-train steppe-lancer-line))
    (can-train shrivamsha-rider-line)
    =>
    (set-goal g-heavy-cavalry-unit shrivamsha-rider-line)
    (disable-self)
)
(defrule
    (not(can-train knight-line))
    (not(can-train hei-guang-cavalry-line))
    (not(can-train steppe-lancer-line))
    (not(can-train shrivamsha-rider-line))
    (can-train 1570)
    =>
    (set-goal g-heavy-cavalry-unit 1570)
    (disable-self)
)
; Alt Cavalry
(defrule
    (can-train camel-line)
    =>
    (set-goal g-alt-cavalry-unit camel-line)
    (disable-self)
)
(defrule
    (not (can-train camel-line))
    (can-train battle-elephant-line)
    =>
    (set-goal g-alt-cavalry-unit battle-elephant-line)
    (disable-self)
)


; Replace Standard Units with Custom Units
(defrule ; Heavy Spearman
    (can-train 892)
    =>
    (set-goal g-spearman-unit 892)
    (disable-self)
)
(defrule ; Heavy Crossbow
    (can-train 493)
    =>
    (set-goal g-archer-unit 493)
    (disable-self)
)
(defrule ; Peltast
    (can-train 2332)
    =>
    (set-goal g-skirmisher-unit 2332)
    (disable-self)
)
(defrule ; Scythian Horse Archer
    (can-train 2485)
    (can-train cavalry-archer-line)
    =>
    (set-goal g-cavalry-archer-unit 2485)
    (disable-self)
)
(defrule ; Khan
    (can-train 1275)
    (can-train cavalry-archer-line)
    =>
    (set-goal g-cavalry-archer-unit 1275)
    (disable-self)
)
(defrule ; Crusader Knight
    (can-train 1723)
    (or
        (can-train knight-line)
        (can-train hei-guang-cavalry-line)
    )
    =>
    (set-goal g-heavy-cavalry-unit 1723)
    (disable-self)
)
(defrule ; Sogdian Cataphract
    (can-train 1299)
    (can-train steppe-lancer-line)
    (not(can-train knight-line))
    (not((can-train hei-guang-cavalry-line)))
    =>
    (set-goal g-heavy-cavalry-unit 1299)
    (disable-self)
)
(defrule ; Greek Noble Cavalry
    (can-train 2322)
    (can-train shrivamsha-rider-line)
    =>
    (set-goal g-heavy-cavalry-unit 2322)
    (disable-self)
)
